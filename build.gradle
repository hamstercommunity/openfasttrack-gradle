plugins {
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'org.sonarqube' version '2.5'
}

repositories {
   jcenter()
   mavenLocal()
}

version = '0.0.1'
group = 'com.github.kaklakariada'

ext {
  gradlePluginId = 'com.github.kaklakariada.openfasttrack'
}

dependencies {
    compile 'com.github.kaklakariada:openfasttrack:0.1.1-alpha'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.47'
}

gradlePlugin {
    plugins {
        openFastTrackPlugin {
            id = gradlePluginId
            implementationClass = 'openfasttrack.gradle.OpenFastTrackPlugin'
        }
    }
}

pluginBundle {
  website = 'https://github.com/hamstercommunity/openfasttrack-gradle/blob/master/README.md'
  vcsUrl = 'https://github.com/hamstercommunity/openfasttrack-gradle'
  description = 'Gradle plugin for tracing requirements using OpenFastTrack'
  tags = ['requirements', 'tracing']

  plugins {
    openFastTrackPlugin {
      id = gradlePluginId
      displayName = 'OpenFastTrack requirements tracing plugin'
    }
  }
}

test {
    testLogging {
        showStandardStreams = true
    }
}

license {
    header = rootProject.file('gradle/license-header.txt')
    strictCheck = true
    skipExistingHeaders = false
    ext.year = 2017
    ext.name = 'Hamster community'
    ext.email = 'christoph at users.sourceforge.net'
}

downloadLicenses {
    includeProjectDependencies = true
    ignoreFatalParseErrors = false
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
