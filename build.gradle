plugins {
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.3'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'org.sonarqube' version '2.6.2'
}

repositories {
   jcenter()
   mavenLocal()
}

version = '0.1.0'
group = 'org.itsallcode'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext {
  gradlePluginId = 'org.itsallcode.openfasttrace'
  oftVersion = '0.6.0'
  if(project.hasProperty('oftSourceDir')) {
    oftSourceDir = file(project.oftSourceDir)
    useOftSources = oftSourceDir.exists()
    if(!useOftSources) {
      logger.warn "OFT source directory $oftSourceDir does not exist"
    }
    oftMavenTargetDir = new File(oftSourceDir, 'target/classes')
  } else {
    oftSourceDir = 'oft-dir-not-found'
    useOftSources = false
  }
}

dependencies {
    if(useOftSources) {
        logger.lifecycle "Including OpenFastTrace sources from Maven target dir $oftMavenTargetDir"
        compile files(oftMavenTargetDir) {
            builtBy "compileOft"
        }
    } else {
        compile "org.itsallcode:openfasttrace:$oftVersion"
    }
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.13.0'
}

task compileOft(type: Exec) {
    workingDir oftSourceDir
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'mvn.cmd', 'resources:resources', 'compiler:compile'
    } else {
        commandLine "mvn", 'resources:resources', 'compiler:compile'
    }
}

clean {
    def exampleProjects = rootProject.file('example-projects').listFiles()
    def propertyFiles = exampleProjects.collect{new File(it, 'gradle.properties')}
    propertyFiles.each { delete it }
}


gradlePlugin {
    plugins {
        openFastTracePlugin {
            id = gradlePluginId
            implementationClass = 'org.itsallcode.openfasttrace.gradle.OpenFastTracePlugin'
        }
    }
}

pluginBundle {
  website = 'https://github.com/itsallcode/openfasttrace-gradle'
  vcsUrl = 'https://github.com/itsallcode/openfasttrace-gradle'
  description = 'Gradle plugin for tracing requirements using OpenFastTrace'
  tags = ['requirements', 'tracing']

  plugins {
    openFastTracePlugin {
      id = gradlePluginId
      displayName = 'OpenFastTrace requirements tracing plugin'
    }
  }
}

test {
    testLogging {
        showStandardStreams = true
    }
}

test.onlyIf { rootProject.name == 'openfasttrace-gradle' }

publishPlugins.dependsOn check

license {
    header = file('gradle/license-header.txt')
    strictCheck = true
    skipExistingHeaders = false
    ext.year = 2017
    ext.name = "It's all code"
    ext.email = 'christoph at users.sourceforge.net'
}

downloadLicenses {
    includeProjectDependencies = true
    ignoreFatalParseErrors = false
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}
